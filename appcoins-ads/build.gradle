apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
android {
  compileSdkVersion COMPILE_SDK_VERSION

  defaultConfig {
    minSdkVersion MIN_SDK_VERSION
    targetSdkVersion TARGET_SDK_VERSION

    versionCode VERSION_CODE
    versionName VERSION_NAME
    buildConfigField 'int', 'ADS_POA_NUMBER_OF_PROOFS', project.ADS_POA_NUMBER_OF_PROOFS
    buildConfigField 'int', 'ADS_POA_PROOFS_INTERVAL_IN_MILIS',
        project.ADS_POA_PROOFS_INTERVAL_IN_MILIS
    buildConfigField 'int', 'ADS_CONNECTIVITY_RETRY_IN_MILLS',
        project.ADS_CONNECTIVITY_RETRY_IN_MILLS
    buildConfigField 'int', 'ADS_CONNECTION_RETRYS_NUMBER',
        project.ADS_CONNECTION_RETRYS_NUMBER
    buildConfigField 'String', 'PROD_BACKEND_BASE_HOST',
        "\"" + project.MAIN_NETWORK_BACKEND_BASE_HOST + "\""
    buildConfigField 'String', 'DEV_BACKEND_BASE_HOST',
        "\"" + project.ROPSTEN_NETWORK_BACKEND_BASE_HOST + "\""
    buildConfigField 'String', 'APPCOINS_POA_SERVICE_NAME',
        "\"" + project.APPCOINS_POA_SERVICE_NAME + "\""

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    consumerProguardFiles 'proguard-rules.pro'
  }

  buildTypes {
    debug {
      minifyEnabled false
      buildConfigField 'String', 'APPCOINS_WALLET_PACKAGE_NAME',
          project.APPCOINS_WALLET_PACKAGE_NAME_DEV
      buildConfigField "String", "APPCOINS_WALLET_ADVERTISEMENT_BIND_ACTION",
          project.APPCOINS_WALLET_ADVERTISEMENT_BIND_ACTION_DEV
      buildConfigField 'String', 'APTOIDE_PACKAGE_NAME',
          project.APTOIDE_PACKAGE_NAME_DEV

      buildConfigField 'String', 'SERVICE_BIND_LIST',
          "\"" + project.APTOIDE_PACKAGE_NAME_DEV.replace('\"', '') +
              "," +
              project.APPCOINS_WALLET_PACKAGE_NAME_DEV.replace('\"', '') +
              "," +
              project.GAMESHUB_PACKAGE_NAME_STG.replace('\"', '') +
              "," +
              project.GAMESHUB_PACKAGE_NAME_DEV.replace('\"', '') +
              "\""
    }
    release {
      minifyEnabled false
      buildConfigField 'String', 'APPCOINS_WALLET_PACKAGE_NAME',
          project.APPCOINS_WALLET_PACKAGE_NAME
      buildConfigField "String", "APPCOINS_WALLET_ADVERTISEMENT_BIND_ACTION",
          project.APPCOINS_WALLET_ADVERTISEMENT_BIND_ACTION
      buildConfigField 'String', 'APTOIDE_PACKAGE_NAME',
          project.APTOIDE_PACKAGE_NAME

      buildConfigField 'String', 'SERVICE_BIND_LIST',
          "\"" + project.APTOIDE_PACKAGE_NAME.replace('\"', '') +
              "," +
              project.APPCOINS_WALLET_PACKAGE_NAME.replace('\"', '') +
              "," +
              project.GAMESHUB_PACKAGE_NAME.replace('\"', '') +
              "\""
      minifyEnabled false
    }
  }

  lintOptions {
    warning 'InvalidPackage'
  }
}

dependencies {
  testImplementation lib.junit
  testImplementation lib.mockito_core

  androidTestImplementation lib.android_test_runner
  api project(':android-appcoins-billing')
  api project(':appcoins-lifecycle')
}

//Publish configuration
task androidSourcesJar(type: Jar) {
  archiveClassifier.set('sources')
  from android.sourceSets.main.java.srcDirs
}

artifacts {
  archives androidSourcesJar
}

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
ext["sonatypeStagingProfileId"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
  println "Found secret props file, loading props"
  Properties p = new Properties()
  p.load(new FileInputStream(secretPropsFile))
  p.each { name, value ->
    ext[name] = value
  }
} else {
  println "No props file, loading env vars"
  ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
  ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
  ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_SECRET_KEY_RING_FILE')
  ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
  ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
  ext["sonatypeStagingProfileId"] = System.getenv('SONATYPE_STAGING_PROFILE_ID')
}

publishing {
  publications {
    release(MavenPublication) {
      groupId PUBLISH_GROUP_ID
      artifactId project.name
      version PUBLISH_VERSION

      artifact("$buildDir/outputs/aar/${project.name}-${System.getenv('BUILD_TYPE_NAME')}.aar")
      artifact androidSourcesJar

      pom {
        name = project.name
        description = 'AppCoins Billing SDK'
        url = 'https://github.com/Catappult/appcoins-sdk/appcoins-ads'
        licenses {
          license {
            name = 'The Apache License, Version 3.0'
            url = 'http://www.apache.org/licenses/LICENSE-3.0.txt'
          }
        }
        developers {
          developer {
            id = 'appcoins.wallet'  
            name = 'AppCoins Wallet Team'
            email = 'wallet@aptoide.com'
          }
        }
        scm {
          connection = 'scm:git:github.com/Catappult/appcoins-sdk.git'
          developerConnection = 'scm:git:ssh://github.com/Catappult/appcoins-sdk.git'
          url = 'https://github.com/Catappult/appcoins-sdk'
        }
        withXml {
          def dependenciesNode = asNode().appendNode('dependencies')

          project.configurations.implementation.allDependencies.each {
            def dependencyNode = dependenciesNode.appendNode('dependency')
            dependencyNode.appendNode('groupId', it.group)
            dependencyNode.appendNode('artifactId', it.name)
            dependencyNode.appendNode('version', it.version)
          }
        }
      }
    }
  }
  repositories {
    maven {
      name = "sonatype"

      def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
      url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

      credentials {
        username OSSRH_USERNAME
        password OSSRH_PASSWORD
      }
    }
  }
}

signing {
  sign publishing.publications
}
//end publish configuration