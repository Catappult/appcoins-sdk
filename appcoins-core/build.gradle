apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'kotlin-android'
apply plugin: 'checkstyle'

android {
    namespace 'com.appcoins.sdk.core'

    compileSdkVersion COMPILE_SDK_VERSION

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION

        versionCode VERSION_CODE
        versionName VERSION_NAME

        consumerProguardFiles 'proguard-rules.pro'

        buildConfigField 'int', 'VERSION_CODE', "${defaultConfig.versionCode}"
        buildConfigField 'String', 'VERSION_NAME', "\"${defaultConfig.versionName}\""
    }

    buildTypes {
        debug {
            minifyEnabled false
            buildConfigField 'String', 'APPCOINS_WALLET_PACKAGE_NAME',
                project.APPCOINS_WALLET_PACKAGE_NAME_DEV
        }
        release {
            buildConfigField 'String', 'APPCOINS_WALLET_PACKAGE_NAME',
                project.APPCOINS_WALLET_PACKAGE_NAME
            minifyEnabled false
        }
    }

    check.dependsOn 'checkstyle', 'lint', 'detekt', 'testDebugUnitTest'

    lintOptions {
        abortOnError false
        warningsAsErrors true
        checkReleaseBuilds true
        disable 'TypographyFractions', 'TypographyQuotes'
        enable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
        warning 'InvalidPackage'
    }

    checkstyle {
        toolVersion = "10.12.4"
        configFile = file("$rootDir/config/checkstyle/checkstyle.xml")
        ignoreFailures = false
        maxWarnings = 0
    }

    tasks.withType(Checkstyle).configureEach {
        reports {
            xml.required = false
            html.required = true
        }
    }

    compileOptions {
        sourceCompatibility SOURCE_COMPATIBILITY
        targetCompatibility TARGET_COMPATIBILITY
    }

    kotlinOptions {
        jvmTarget = TARGET_COMPATIBILITY
    }
}

dependencies {
    testImplementation libs.mockk
    testImplementation libs.robolectric
    testImplementation libs.kotlin.test.junit

    detektPlugins libs.detekt.formatting

    implementation libs.kotlin.stdlib

    api libs.indicative
    api libs.matomo.android
}

// Publish configuration
task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
}

publishing {
    publications {
        release(MavenPublication) {
            groupId PUBLISH_GROUP_ID
            artifactId project.name
            version PUBLISH_VERSION

            artifact("$buildDir/outputs/aar/${project.name}-${System.getenv('BUILD_TYPE_NAME')}.aar")
            artifact androidSourcesJar

            pom {
                name = project.name
                description = 'AppCoins Billing SDK'
                url = 'https://github.com/Catappult/appcoins-sdk/appcoins-core'
                licenses {
                    license {
                        name = 'The Apache License, Version 3.0'
                        url = 'http://www.apache.org/licenses/LICENSE-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'appcoins.wallet'
                        name = 'AppCoins Wallet Team'
                        email = 'wallet@aptoide.com'
                    }
                }
                scm {
                    connection = 'scm:git:github.com/Catappult/appcoins-sdk.git'
                    developerConnection = 'scm:git:ssh://github.com/Catappult/appcoins-sdk.git'
                    url = 'https://github.com/Catappult/appcoins-sdk'
                }
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.implementation.allDependencies.each {
                        if (it.name != "unspecified") {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
}
//end publish configuration
