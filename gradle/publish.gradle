ext {
    VERSION_CODE = 143
    VERSION_NAME_SUFFIX = System.getenv("BUILD_NAME_SUFFIX") != null ? System.getenv("BUILD_NAME_SUFFIX") : ""
    VERSION_NAME = "0.9.0.12" + VERSION_NAME_SUFFIX
    PUBLISH_VERSION = VERSION_NAME
    PUBLISH_GROUP_ID = "io.catappult"
}

plugins.withId('maven-publish') {
    apply plugin: 'signing'

    // Signing credentials from env or local.properties
    ext["signing.keyId"] = ''
    ext["signing.password"] = ''
    ext["signing.secretKeyRingFile"] = ''
    ext["ossrhUsername"] = ''
    ext["ossrhPassword"] = ''

    File secretPropsFile = rootProject.file("local.properties")
    if (secretPropsFile.exists()) {
        Properties p = new Properties()
        p.load(new FileInputStream(secretPropsFile))
        p.each { name, value -> ext[name] = value }
    } else {
        ext["signing.keyId"] = System.getenv("SIGNING_KEY_ID")
        ext["signing.password"] = System.getenv("SIGNING_PASSWORD")
        ext["signing.secretKeyRingFile"] = System.getenv("SIGNING_SECRET_KEY_RING_FILE")
        ext["ossrhUsername"] = System.getenv("OSSRH_USERNAME")
        ext["ossrhPassword"] = System.getenv("OSSRH_PASSWORD")
    }

    signing {
        required { !version.endsWith('SNAPSHOT') }
        sign publishing.publications
    }
}
